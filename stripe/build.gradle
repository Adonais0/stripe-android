plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'checkstyle'

    // make sure this line comes *after* you apply the Android plugin
    id 'com.getkeepsafe.dexcount'

    id 'maven'
    id 'signing'
    id 'maven-publish'
}

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

configurations {
    javadocDeps
    ktlint
}

dependencies {
    implementation "com.android.support:support-annotations:28.0.0"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    // Api for this import because we use reflection to alter TextInputLayout
    api "com.android.support:design:28.0.0"

    implementation "com.stripe:stripe-3ds2-android:1.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    javadocDeps "com.android.support:support-annotations:28.0.0"
    javadocDeps "com.android.support:appcompat-v7:28.0.0"
    javadocDeps "com.android.support:design:28.0.0"
    compileOnly "javax.annotation:jsr250-api:${rootProject.ext.javaAnnotationVersion}"

    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.json:json:20190722'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    ktlint 'com.pinterest:ktlint:0.36.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion rootProject.ext.compileSdkVersion
        consumerProguardFiles 'proguard-rules.txt'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        main.kotlin.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/java'

        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
    productFlavors {
    }

    lintOptions {
        lintConfig file("../settings/lint.xml")
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply from: 'deploy.gradle'
